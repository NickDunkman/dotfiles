export DOTFILES=$HOME/Code/NickDunkman/dotfiles

# source zgen
source $DOTFILES/zgen/zgen.zsh
if ! zgen saved; then
  # specify plugins here
  zgen oh-my-zsh
  zgen oh-my-zsh plugins/git
  zgen oh-my-zsh plugins/autojump
  zgen oh-my-zsh plugins/vscode
  zgen oh-my-zsh plugins/docker
  zgen oh-my-zsh plugins/docker-compose
  zgen load unixorn/autoupdate-zgen
  zgen load romkatv/powerlevel10k powerlevel10k

  # generate the init script from plugins above
  zgen save
fi

# grab all .zsh files (not in the zgen directory)
setopt EXTENDED_GLOB
typeset -U config_files
config_files=($DOTFILES/(^zgen/)#*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
